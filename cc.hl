#(ctimex (define debug-display-include-paths #t))
#(usedll CLikeSCore)

include "./clike/backend/ll-ast.hl"

litinclude ("./doc/inline") "./verilog/inline.hl"
litinclude ("./doc/emit") "./backends/small1/emit.hl"

define small1_cmdline_imul = mkref(true);

function runssaopt(env, mdl) {
 visit:ir2(irmodule: mdl) {
      deep irtoplevel {
          %function -> if (body) {
            <g2tp:g2> = ll_genssa_opt(env, node);
             return g2} else node
       |  else -> node}}}

function ir2backend_small1(mdl, fnm)
{
 try {
  try {
    small1_enable_mul := ^small1_cmdline_imul;
    ir2mdl = map m in mdl do %ir0->ir2(m);
    p1 = ir2_bodypass(ir2mdl, il2ssa_fullssa);
    px = if (deref( %llvm-optimise ))
           runssaopt(ll_module_types(ir2mdl), p1)
         else p1;
    p2 = small1_backend_passes(px,fnm);
    return p1
  } catch (t_MBaseException e) {
    println(mbaseerror(e));
    println(%->s(e));
    exit(-1);
  }
 } catch (t_Exception e) {
   println(%->s(e));
   exit(-1)
 }
}

function clike_compile_files(mdname, fls)
{
  nollvm = true;
  mdl = llvm_init_module(mdname);
  env = clike_global_env();
  %read-int-eval('define'('clike-current-env',#`(quote ,env)));
  %read-int-eval('define'('clike-current-llvm-env',#`(quote ,mdl)));
  iter fls do {
     println(%S<<("Compiling: ",fls));
     match fls with
       c(fn) -> clike_compile_file(mdl,env,fn)
     | x(fn) -> clike_bypass_file(mdl,env,fn)
  };
  llvm_save(mdl, mdname);
  println("Done.");
}
  

function main()
collector(fadd,fget) {
 %read-int-eval(#'(n.module clikeccrepl dll));
 mname = mkref("test");
 %llvm-optimise := [];
 ir2backend_hook := ir2backend_small1;
 do loop(a = %a->l( %*CMDLINE* )) {
    match a with
      ["/out";mdlname;@rest] -> {mname := mdlname;loop(rest)}
    | ["/imul";@rest] -> {small1_cmdline_imul := true; loop(rest)}
    | ["/noimul";@rest] -> {small1_cmdline_imul := []; loop(rest)}
    | ["/d";dllname;@rest] -> {%read-compile-eval('usedll'(%Sm<<(dllname)));
                               loop(rest);}
    | ["/o+";@rest] -> { %llvm-optimise := true; loop(rest)}
    | ["/o-";@rest] -> { %llvm-optimise := []; loop(rest)}
    | ["/dbg";levl;@rest] -> {clike_debug_level := %S->N(levl); loop(rest)}
    | ["/s";hlfile;@rest] -> {%read-compile-eval('hlevl-file'(hlfile)); loop(rest)}
    | ["/a";alfile;@rest] -> {%read-compile-eval('include'(alfile)); loop(rest)}
    | ["/i";@rest] -> {clike_no_llvm := true;loop(rest)}
    | ["/x";fname;@rest] -> {fadd('x'(fname));loop(rest)}
    | [srcfile;@rest] -> {fadd('c'(srcfile));loop(rest)}
    | [] -> {
       fls = fget();
       if(not(fls)) {
         iter(println,
               ["Usage: clikecc [option...] file.c...";
                "Options:";
                "   /out <modulename>   : save bitcode to a given module";
                "   /imul               : enable IMUL and the other extended instructions";
                "   /noimul             : disable IMUL and the other extended instructions";
                "   /d   <dllname>      : load a given MBase dll";
                "   /s   <srcfile>      : load a given PFront source file";
                "   /a   <srcfile>      : load a given MBase source file";
                "   /x   <file>         : compile in an intermediate LLVM code";
                "   /i                  : save an output in an intermediate format";
                "   /o+                 : enable optimisation";
                "   /o-                 : disable optimisation";
                "   /dbg <level>        : enable debugging output (1-8)"]);
         quit();
       } 
         else clike_compile_files(^mname,fls);
    }
 }
}

